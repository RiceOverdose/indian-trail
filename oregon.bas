 10 REM PROGRAM NAME - OREGON        VERSION:01/01/78
 20 REM ORIGINAL PROGRAMMING BY BILL HEINEMANN - 1971
 30 REM SUPPORT RESEARCH AND MATERIALS BY DON RAWITSCH,
 40 REM      MINNESOTA EDUCATIONAL COMPUTING CONSORTIUM STAFF
 50 REM CDC CYBER 70/73-26     BASIC 3.1
 60 REM DOCUMENTATION BOOKLET 'OREGON' AVAILABLE FROM
 61 REM    MECC SUPPORT SERVICES
 62 REM    2520 BROADWAY DRIVE
 63 REM    ST. PAUL, MN  55113
 80 REM
 150 REM *FOR THE MEANING OF THE VARIABLES USED, LIST LINES 6470-6790*
 155 OPTION GOSUB
 160 PRINT "DO YOU NEED INSTRUCTIONS (YES/NO)";
 180 RANDOMIZE , 5
 190 INPUT C$
 195 C$=UCASE$(C$)
 200 IF C$="NO" THEN 690
 240 PRINT "THIS PROGRAM SIMULATES ST. THOMAS'S MISSIONARY JOURNEY"
 250 PRINT "FROM JERUSALEM, ISRAEL TO MYLAPORE, INDIA IN AD 72."
 260 PRINT "YOU AND YOUR DISCIPLES WILL COVER THE 3400 MILE JOURNEY"
 270 PRINT "IN 5-6 YEARS --- IF YOU MAKE IT ALIVE."
 280 PRINT
 290 PRINT "YOU HAD SAVED 10 DENARII TO SPEND FOR THE TRIP, AND YOU'VE JUST"
 300 PRINT "   PAID 3 DENARII FOR A PAIR OF SHOES."
 310 PRINT "YOU WILL NEED TO SPEND THE REST OF YOUR MONEY ON THE"
 320 PRINT "   FOLLOWING ITEMS:"
 330 PRINT
 340 PRINT "     FOLLOWERS - YOU CAN SPEND 2-3 DENARII ON YOUR FOLLOWERS"
 350 PRINT "            THE MORE YOU SPEND, THE FASTER YOU'LL GO"
 360 PRINT "            BECAUSE YOU'LL HAVE BETTER ENCOURAGEMENT"
 370 PRINT
 380 PRINT "     THE HOLY SPIRIT - THE MORE YOU HAVE, THE LESS CHANCE THERE"
 390 PRINT "                IS OF GETTING PERSECUTED, THIS IS NEEDED" 
 391 PRINT "                         FOR SPIRITUAL EATING"
 400 PRINT
 410 PRINT "     BIBLES - 0.5 DENARII BUYS A PACK OF 2500 BIBLES"
 420 PRINT "            YOU WILL NEED BIBLES FOR EVANGELIZING"
 430 PRINT "     TO WARD OFF EVIL SPIRITS, AND FOR SPREADING THE WORD"
 440 PRINT
 450 PRINT "     ARMOR OF GOD - THIS IS ESPECIALLY IMPORTANT FOR THE PAGANS"
 460 PRINT "               YOU WILL ENCOUNTER ON THE JOURNEY"
 480 PRINT
 490 PRINT "     MISCELLANEOUS SPIRITUAL SUPPLIES - THIS INCLUDES MEDICINE"
 500 PRINT "              OTHER THINGS YOU WILL NEED FOR SICKNESS"
 510 PRINT "              AND EMERGENCY REPAIRS"
 520 PRINT
 530 PRINT
 540 PRINT "YOU CAN SPEND ALL YOUR MONEY BEFORE YOU START YOUR TRIP -"
 550 PRINT "OR YOU CAN SAVE SOME OF YOUR DENARII TO SPEND AT CITIES ALONG"
 560 PRINT "THE WAY WHEN YOU RUN LOW. HOWEVER, ITEMS COST MORE AT"
 570 PRINT "THE FORTS. YOU CAN ALSO GO EVANGELIZING ALONG THE WAY TO GET"
 580 PRINT "MORE HOLY SPIRIT."
 590 PRINT "WHENEVER YOU HAVE TO USE YOUR TRUSTY BIBLE ALONG THE WAY,"
 600 PRINT "YOU WILL BE TOLD TO TYPE IN THAT WORD (ONE THAT SOUNDS LIKE A"
 610 PRINT "TRUE BELIEVER). THE FASTER YOU TYPE IN THAT WORD AND HIT THE"
 620 PRINT """RETURN"" KEY, THE BETTER LUCK YOU'LL HAVE WITH YOUR EVANGELIZATION."
 630 PRINT
 640 PRINT "AT EACH TURN, ALL ITEMS ARE SHOWN IN DENARIUS AMOUNTS"
 650 PRINT "EXCEPT BIBLES"
 670 PRINT
 680 PRINT "GOOD LUCK!!!"
 690 PRINT
 700 PRINT
 710 PRINT "HOW GOOD A PREACHER ARE YOU WITH YOUR BIBLE?"
 720 PRINT "    (1) BASICALLY A PASTOR,  (2) FIRST YEAR SEMINARIAN,"
 730 PRINT "   (3) WENT TO CONCORDIA,  (4) CHURCHGOER,  (5) WHO IS JESUS?"
 740 PRINT "ENTER ONE OF THE ABOVE -- THE BETTER YOU CLAIM YOU ARE, THE"
 750 PRINT "FASTER YOU'LL HAVE TO BE WITH YOUR BIBLE TO BE SUCCESSFUL."
 760 INPUT D9
 770 IF D9>5 THEN 790
 780 GOTO 810
 790 D9=0
 800 REM ^*INITIAL PURCHASES^*
 810 X1=-1
 820 K5=0
 830 PRINT
 840 PRINT
 850 PRINT "HOW MUCH DO YOU WANT TO SPEND ON YOUR FOLLOWERS";
 860 INPUT A
 870 IF A >= 2 THEN 900
 880 PRINT "NOT ENOUGH"
 890 GOTO 850
 900 IF A <= 3 THEN 930
 910 PRINT "TOO MUCH"
 920 GOTO 850
 930 PRINT "HOW MUCH DO YOU WANT TO SPEND ON THE HOLY SPIRIT";
 940 INPUT F
 950 IF F >= 0 THEN 980
 960 PRINT "IMPOSSIBLE"
 970 GOTO 930
 980 PRINT "HOW MUCH DO YOU WANT TO SPEND ON BIBLES";
 990 INPUT B
1000 IF B >= 0 THEN 1030
1010 PRINT "IMPOSSIBLE"
1020 GOTO 980
1030 PRINT "HOW MUCH DO YOU WANT TO SPEND ON ARMOR OF GOD";
1040 INPUT C
1050 IF C >= 0 THEN 1080
1060 PRINT "IMPOSSIBLE"
1070 GOTO 1030
1080 PRINT "HOW MUCH DO YOU WANT TO SPEND ON MISCELLANEOUS SUPPLIES";
1090 INPUT M1
1100 IF M1 >= 0 THEN 1130
1110 PRINT "IMPOSSIBLE"
1120 GOTO 1080
1130 T=7-A-F-B-C-M1
1140 IF T >= 0 THEN 1170
1150 PRINT "YOU OVERSPENT--YOU ONLY HAD 7 DENARII TO SPEND. BUY AGAIN"
1160 GOTO 830
1170 B=50*B
1171 B=B*100
1172 A=A*100
1173 F=F*100
1174 C=C*100
1175 M1=M1*100
1180 PRINT "AFTER ALL YOUR PURCHASES, YOU NOW HAVE ";T;" DENARII LEFT"
1190 PRINT
1200 PRINT "MARCH 23, 72 AD"
1210 PRINT
1220 GOTO 1750
1230 IF M >= 3400 THEN 5430
1240 REM ^*SETTING DATE^*
1250 D3=D3+1
1260 PRINT
1270 PRINT "MONDAY ";
1280 IF D3>10 THEN 1300
1290 ON D3 GOTO 1310, 1330, 1350, 1370, 1390, 1410, 1430, 1450, 1470, 1490
1300 ON D3-10 GOTO 1510,1530,1550,1570,1590, 1610, 1630, 1650,  1652,  1654,  1656,  1658,  1660, 1671, 1673, 1675, 1677, 1681, 1690
1310 PRINT "APRIL 2, 72 AD";
1320 GOTO 1720
1330 PRINT "APRIL 9, 72 AD";
1340 GOTO 1720
1350 PRINT "APRIL 13, 72 AD";
1360 GOTO 1720
1370 PRINT "APRIL 17, 72 AD";
1380 GOTO 1720
1390 PRINT "APRIL 23, 72 AD";
1400 GOTO 1720
1410 PRINT "APRIL 30, 72 AD";
1420 GOTO 1720
1430 PRINT "MAY 1, 72 AD";
1440 GOTO 1720
1450 PRINT "MAY 4, 72 AD";
1460 GOTO 1720
1470 PRINT "MAY 12, 72 AD";
1480 GOTO 1720
1490 PRINT "MAY 20, 72 AD";
1500 GOTO 1720
1510 PRINT "MAY 27, 72 AD";
1520 GOTO 1720
1530 PRINT "JUNE 2, 72 AD";
1540 GOTO 1720
1550 PRINT "JUNE 6, 72 AD";
1560 GOTO 1720
1570 PRINT "JUNE 15, 72 AD";
1580 GOTO 1720
1590 PRINT "JUNE 29, 72 AD";
1600 GOTO 1720
1610 PRINT "JULY 3, 72 AD";
1620 GOTO 1720
1630 PRINT "JULY 14, 72 AD";
1640 GOTO 1720
1650 PRINT "JULY 22, 72 AD";
1651 GOTO 1720
1652 PRINT "JULY 31, 72 AD";
1653 GOTO 1720
1654 PRINT "AUGUST 12, 72 AD";
1655 GOTO 1720
1656 PRINT "AUGUST 26, 72 AD";
1657 GOTO 1720
1658 PRINT "SEPTEMBER 10, 72 AD";
1659 GOTO 1720
1660 PRINT "SEPTEMBER 22, 72 AD";
1670 GOTO 1720
1671 PRINT "OCTOBER 5, 72 AD";
1672 GOTO 1720
1673 PRINT "OCTOBER 25, 72 AD";
1674 GOTO 1720
1675 PRINT "NOVEMBER 11, 72 AD";
1676 GOTO 1720
1677 PRINT "NOVEMBER 17, 72 AD";
1678 GOTO 1720
1679 PRINT "DECEMBER 1, 72 AD";
1680 GOTO 1720
1681 PRINT "DECEMBER 20, 72 AD";
1682 GOTO 1720
1690 PRINT "YOU HAVE BEEN ON THE MISSION TOO LONG ------"
1700 PRINT "YOU AND YOUR FOLLOWERS DIE AT THE HANDS OF SOME PARTICULARLY STUBBORN AND AGGRESIVE PAGANS"
1710 GOTO 5170
1720 PRINT ""
1730 PRINT
1740 REM ^*BEGINNING EACH TURN^*
1750 IF F >= 0 THEN 1770
1760 F=0
1770 IF B >= 0 THEN 1790
1780 B=0
1790 IF C >= 0 THEN 1810
1800 C=0
1810 IF M1 >= 0 THEN 1830
1820 M1=0
1830 IF F >= 13 THEN 1910
1840 PRINT "YOU'D BETTER DO SOME EVANGELIZING OR BUY HOLY SPIRIT SOON!!!!"
1910 M2=INT(M)
1920 IF S4=1 THEN 1950
1930 IF K8=1 THEN 1950
1940 GOTO 1990
1950 T=T-0.5
1960 IF T<0 THEN 5080
1970 PRINT "JESUS' BILL IS 0.5 DENARII"
1980 LET K8=S4=0
1990 IF M9=1 THEN 2020
2000 PRINT "TOTAL MILEAGE IS";INT(M)
2010 GOTO 2040
2020 PRINT "TOTAL MILEAGE IS 950"
2030 M9=0
2040 PRINT "HOLY SPIRIT","BIBLES","ARMOR OF GOD","MISC. SUPP.","DENARII"
2050 PRINT INT(F),INT(B),INT(C),INT(M1),T
2060 IF X1=-1 THEN 2170
2070 X1=X1*(-1)
2080 PRINT "DO YOU WANT TO (1) STOP AT THE NEXT CITY, (2) EVANGELIZE, ";
2090 PRINT "OR (3) CONTINUE"
2100 INPUT X
2110 IF X>2 THEN 2150
2120 IF X<1 THEN 2150
2130 LET X=INT(X)
2140 GOTO 2270
2150 LET X=3
2160 GOTO 2270
2170 PRINT "DO YOU WANT TO (1) EVANGELIZE, OR (2) CONTINUE"
2180 INPUT X
2190 IF X=1 THEN 2210
2200 LET X=2
2210 LET X=X+1
2220 IF X=3 THEN 2260
2230 IF B>39 THEN 2260
2240 PRINT "TOUGH---YOU NEED MORE BIBLES TO GO EVANGELIZING"
2250 GOTO 2170
2260 X1=X1*(-1)
2270 ON X GOTO 2290,2540,2720
2280 REM ^*STOPPING AT FORT^*
2290 PRINT "ENTER WHAT YOU WISH TO SPEND ON THE FOLLOWING"
2300 PRINT "HOLY SPIRIT";
2310 GOSUB 2330
2320 GOTO 2410
2330 INPUT P
2340 IF P<0 THEN 2400
2350 T=T-P
2360 IF T >= 0 THEN 2400
2370 PRINT "YOU DON'T HAVE THAT MUCH--KEEP YOUR SPENDING DOWN"
2375 PRINT "YOU MISS YOUR CHANCE TO SPEND ON THAT ITEM"
2380 T=T+P
2390 P=0
2400 RETURN
2410 F=F+2/3*P
2420 PRINT "BIBLES";
2430 GOSUB 2330
2440 LET B=B+2/3*P*50
2450 PRINT "ARMOR OF GOD";
2460 GOSUB 2330
2470 C=C+2/3*P
2480 PRINT "MISCELLANEOUS SUPPLIES";
2490 GOSUB 2330
2500 M1=M1+2/3*P
2510 M=M-45
2520 GOTO 2720
2530 REM ^*EVANGELIZING^*
2540 IF B>39 THEN 2570
2550 PRINT "TOUGH---YOU NEED MORE BIBLES TO GO EVANGELIZING"
2560 GOTO 2080
2570 M=M-45
2580 GOSUB 6140
2590 IF B1 <= 1 THEN 2660
2600 IF 100*RND(-1)<13*B1 THEN 2710
2610 F=F+48-2*B1
2620 PRINT "NICE EVANGELIZING--RIGHT ON TARGET--GOOD SPIRITUAL EATIN' TONIGHT!!"
2621 PRINT "YOU TALKED TO THE PAGANS ABOUT THE COSMOLOGICAL ARGUMENT OF CHRISTIANITY"
2622 PRINT "BECAUSE THEY APPRECIATE REASON AND THE COSMOS, THEY LISTEN TO YOU"
2623 PRINT "YOU EXPLAIN THAT: "
2624 PRINT "  1. WHATEVER BEGINS TO EXIST HAS A CAUSE."
2625 PRINT "  2. THE UNIVERSE BEGAN TO EXIST"
2626 PRINT "  3. THEREFORE THE UNIVERSE HAS A CAUSE"
2627 PRINT "AFTER LISTENING, THEY REALISE THAT THEY WANT TO KNOW MORE!"
2628 PRINT "YOU CONVERTED THEM TO CHRISTIANITY AND FOUNDED A CHURCH!"
2630 B=B-10-3*B1
2640 GOTO 2720
2650 REM ^BELLS IN LINE 2660^
2660 PRINT !"\007RIGHT BETWEEN THE EYES---YOU GOT A BIG ONE!!!!"
2670 PRINT !"\007FULL SPIRITUAL BELLIES TONIGHT!"
2671 PRINT "YOU TALKED TO THE PAGANS ABOUT THE EARLY CHURCH ARGUMENT OF CHRISTIANITY"
2672 PRINT "BECAUSE THEY APPRECIATE REASON AND HISTORY, THEY LISTEN TO YOU"
2673 PRINT "YOU EXPLAIN THAT: "
2674 PRINT "  THE EARLY DISCIPLES HISTORICALLY BEGAN TO PREACH AND SPREAD THE GOSPEL"
2675 PRINT "  DESPITE EVERY OTHER ACCOUNT SAYING THE CONTRARY"
2676 PRINT "  AND MANY OTHERS OUTSIDE THE TWELVE RECORDED SEEING JESUS BEFORE THE ASCENSION"
2677 PRINT "AFTER LISTENING, THEY REALISE THAT THEY WANT TO KNOW MORE!"
2678 PRINT "YOU CONVERTED THEM TO CHRISTIANITY AND FOUNDED A CHURCH!"
2680 F=F+52+RND(-1)*6
2690 B=B-10-RND(-1)*4
2700 GOTO 2720
2710 PRINT "YOU FAILED---AND YOUR SPIRITUAL DINNER GOT AWAY....."
2711 PRINT "THE PAGANS SAY TO YOU THAT CHRISTIANITY, LIKE OTHER GOD-CENTERED RELIGIONS, ARE JUST TRYING"
2712 PRINT "TO COME UP WITH EXPLANATIONS TO SATISFY THEIR OWN MINDS"
2713 PRINT "AND THAT JUST BECAUSE SCIENTIFICALLY, SOMETHING ISN'T KNOWN OR PROVEN YET"
2714 PRINT "THAT DOESN'T MEAN THAT THERE MUST BE A THEOLOGICAL OR SUPERNATURAL REASON BEHIND IT"
2715 PRINT "YOU START STAMMERING AND HAVE NO IDEA HOW TO RESPOND"
2716 PRINT "YOUR FAITH IS DIMINISHED AND YOU ARE SPIRITUALLY STARVING"
2720 IF F >= 13 THEN 2750
2730 GOTO 5060
2740 REM ^*EATING^*
2750 PRINT "DO YOU WANT TO EAT SPIRITUALLY: (1) POORLY (2) MODERATELY"
2760 PRINT "OR (3) WELL";
2770 INPUT E
2780 IF E>3 THEN 2750
2790 IF E<1 THEN 2750
2800 LET E=INT(E)
2810 LET F=F-8-5*E
2820 IF F >= 0 THEN 2860
2830 F=F+8+5*E
2840 PRINT "YOU CAN'T SPIRITUALLY EAT THAT WELL"
2850 GOTO 2750
2860 LET M=M+200+(A-220)/3+10*RND(-1)
2870 L1=C1=0
2880 REM ^*RIDERS ATTACK^*
2890 IF RND(-1)*10>((M/100-4)^2+72)/((M/100-4)^2+12)-1 THEN 3550
2900 PRINT "PAGANS AHEAD.  THEY ";
2910 S5=0
2920 IF RND(-1)<.8 THEN 2950
2930 PRINT "DON'T ";
2940 S5=1
2950 PRINT "LOOK HOSTILE"
2960 PRINT "TACTICS"
2970 PRINT "(1) RUN  (2) EVANGELIZE  (3) CONTINUE  (4) COMMAND FOLLOWERS INTO FORMATION"
2980 IF RND(-1)>.2 THEN 3000
2990 S5=1-S5
3000 INPUT T1
3010 IF T1<1 THEN 2970
3020 IF T1>4 THEN 2970
3030 T1=INT(T1)
3040 IF S5=1 THEN 3330
3050 IF T1>1 THEN 3110
3060 M=M+20
3070 M1=M1-15
3080 B=B-150
3090 A=A-40
3100 GOTO 3470
3110 IF T1>2 THEN 3240
3120 GOSUB 6140
3130 B=B-B1*40-80
3140 IF B1>1 THEN 3170
3150 PRINT "NICE EVANGELIZING---YOU CONVERTED THEM, EXPLAINING THE TRINITY IN DETAIL"
3151 PRINT "BECAUSE THEY WERE HINDUS, THEY ALSO BELIEVED IN A TRINITY (THE TRIMURTI), WHICH MADE IT EASIER"
3152 PRINT "TO EXPLAIN THE CONCEPT OF THE TRINITY!"
3153 PRINT "JUST LIKE HOW BRAHMA IS THE SUPREME FATHER, GOD THE FATHER ALSO CREATED THE UNIVERSE"
3154 PRINT "JUST LIKE HOW VISHNU IS THE INCARNATION OF GOD, JESUS, GOD THE SON, IS ALSO THE PHYSICAL MANIFESTATION OF GOD"
3155 PRINT "JUST LIKE HOW SHIVA IS THE SUPREME SOUL, THE HOLY SPIRIT IS ALSO PRESENT IN OUR SOULS"
3156 PRINT "HOWEVER, SOME CORE BELIEFS IN LIFE, DEATH, HEAVEN, HELL, AND REINCARNATION ARE STILL DIFFERENT"
3160 GOTO 3470
3170 IF B1 <= 4 THEN 3220
3180 PRINT "LOUSY PREACHER---YOU GOT REFUTED"
3181 PRINT "BECAUSE THEY WERE HINDUS, THEY BELIEVED IN DIVINITY IN ALL AND ALL IN DIVINITY"
3182 PRINT "THEY ARGUED AGAINST YOUR MONOTHEISTIC BELIEFS SAYING THAT IF HUMANS WERE CREATED IN THE IMAGE OF GOD"
3183 PRINT "THAT JUST MEANS WE NEED TO RELEASE THE SIN AND WORLDLY THINGS THAT BIND US TO BECOME GODS"
3184 PRINT "THEY ARGUE ABOUT THE IMPORTANCE OF REINCARNATION IN DISCOVERING NEW TRUTHS ABOUT OURSELVES"
3185 PRINT "AND YOU CAN'T FIND ANY COUNTERARGUMENTS TO CONVINCE THEM!"
3186 PRINT "YOUR FOLLOWERS SEEM VERY DISAPPOINTED IN YOU"
3190 K8=1
3200 PRINT "YOU HAVE TO SEE JESUS"
3210 GOTO 3470
3220 PRINT "KINDA SLOW WITH YOUR LUTHERAN STUDY BIBLE"
3230 GOTO 3470
3240 IF T1>3 THEN 3290
3250 IF RND(-1)>.8 THEN 3450
3260 LET B=B-150
3270 M1=M1-15
3280 GOTO 3470
3290 GOSUB 6140
3300 B=B-B1*30-80
3310 M=M-25
3320 GOTO 3140
3330 IF T1>1 THEN 3370
3340 M=M+15
3350 A=A-10
3360 GOTO 3470
3370 IF T1>2 THEN 3410
3380 M=M-5
3390 B=B-100
3400 GOTO 3470
3410 IF T1>3 THEN 3430
3420 GOTO 3470
3430 M=M-20
3440 GOTO 3470
3450 PRINT "THEY DID NOT ATTACK"
3460 GOTO 3550
3470 IF S5=0 THEN 3500
3480 PRINT "PAGANS WERE FRIENDLY, BUT CHECK FOR POSSIBLE LOSSES"
3490 GOTO 3550
3500 PRINT "PAGANS WERE HOSTILE--CHECK FOR LOSSES"
3510 IF B >= 0 THEN 3550
3520 PRINT "YOU RAN OUT OF BIBLES AND GOT MARTYRED BY THE PAGANS"
3530 GOTO 5170
3540 REM ^*SELECTION OF EVENTS^*
3550 LET D1=0
3560 RESTORE
3570 R1=100*RND(-1)
3580 LET D1=D1+1
3590 IF D1=16 THEN 4670
3600 READ D
3610 IF R1>D THEN 3580
3620 DATA 6,11,13,15,17,22,32,35,37,42,44,54,64,69,95
3630 IF D1>10 THEN 3650
3640 ON D1 GOTO 3660,3700,3740,3790,3820,3850,3880,3960,4130,4190
3650 ON D1-10 GOTO 4220,4290,4340,4560,4610,4670
3660 PRINT "FOLLOWER BREAKS DOWN--LOSE TIME AND SUPPLIES FIXING THEM"
3670 LET M=M-15-5*RND(-1)
3680 LET M1=M1-8
3690 GOTO 4710
3700 PRINT "FOLLOWER INJURES LEG---SLOWS YOU DOWN REST OF TRIP"
3710 LET M=M-25
3720 LET A=A-20
3730 GOTO 4710
3740 PRINT "BAD LUCK---YOUR BIBLE BROKE ITS BINDING"
3750 PRINT "YOU HAD TO STOP AND USE SUPPLIES TO MAKE A NEW ONE"
3760 M=M-5-4*RND(-1)
3770 M1=M1-2-3*RND(-1)
3780 GOTO 4710
3790 PRINT "FOLLOWER WANDERS OFF---SPEND TIME LOOKING FOR THEM"
3800 M=M-17
3810 GOTO 4710
3820 PRINT "YOUR BIBLES GET LOST---SPEND HALF THE DAY LOOKING FOR THEM"
3830 M=M-10
3840 GOTO 4710
3850 PRINT "DEATH WATER--LOSE TIME LOOKING FOR LIVING SPRING"
3860 LET M=M-10*RND(-1)-2
3870 GOTO 4710
3880 IF M>950 THEN 4490
3890 PRINT "HEAVY SPIRITUAL RAINS---TIME AND SUPPLIES LOST"
3910 F=F-10
3920 B=B-500
3930 M1=M1-15
3940 M=M-10*RND(-1)-5
3950 GOTO 4710
3960 PRINT "ROWDY PAGANS ATTACK"
3970 GOSUB 6140
3980 B=B-20*B1
3990 IF B >= 0 THEN 4030
4000 PRINT "YOU RAN OUT OF BIBLES---THEY GET LOTS OF DENARII"
4010 T=T/3
4020 GOTO 4040
4030 IF B1 <= 1 THEN 4100
4040 PRINT "YOU GOT REFUTED AND THEY TOOK ONE OF YOUR FOLLOWERS"
4050 K8=1
4060 PRINT "BETTER HAVE JESUS LOOK AT YOUR FAITH"
4070 M1=M1-5
4080 A=A-20
4090 GOTO 4710
4100 PRINT "QUICKEST EVANGELIZING OUTSIDE OF JERUSALEM!!!"
4110 PRINT "YOU GOT 'EM!"
4120 GOTO 4710
4130 PRINT "THERE WAS A SPIRITUAL FIRE IN YOUR WAGON--HOLY SPIRIT AND SUPPLIES DAMAGE!"
4140 F=F-40
4150 B=B-400
4160 LET M1=M1-RND(-1)*8-3
4170 M=M-15
4180 GOTO 4710
4190 PRINT "LOSE YOUR WAY IN POLYTHEISTIC EMPIRES---TIME IS LOST"
4200 M=M-10-5*RND(-1)
4210 GOTO 4710
4220 PRINT "YOU EVANGELIZED A PAGAN AFTER THEY TRIED TO PERSECUTE YOU"
4221 PRINT "THEY TRIED TO USE THE SCIENTIFIC EVIDENCE LIKE EVOLUTION AND THE EXPANDING UNIVERSE TO SHOW HOW CREATION"
4222 PRINT "AND THE OTHER BIBLICAL STORIES ARE NOT PLAUSIBLE"
4223 PRINT "YOU KINDLY ARGUE THAT: "
4224 PRINT "THERE IS ACTUALLY A LOT OF SCIENTIFIC PROOF FOR THE EXISTENCE OF GOD"
4225 PRINT "FOR EXAMPLE, THE CREATION OF THE UNIVERSE AND THE BEINGS INSIDE OF IT ARE WAY TOO INTRICATE TO BE ACCIDENTAL"
4226 PRINT "AND THE LACK OF CONNECTIONS IN THE SCIENTIFIC ARGUMENT AGAINST CHRISTIANITY, ESPECIALLY EVOLUTION, GIVE CREDANCE TO THE GODLY CREATION STORY"
4227 PRINT "AFTER LISTENING, THEY REALISE THAT THEY HAVE NO MORE ARGUMENTS AGAINST CHRISTIANITY!"
4228 PRINT "THEY DECIDE THAT THEY WANT TO FOLLOW JESUS!"
4229 PRINT "YOU CONVERTED THEM TO CHRISTIANITY AND FOUNDED A CHURCH!"
4230 B=B-10
4240 M1=M1-5
4250 IF M1 >= 0 THEN 4280
4260 PRINT "YOU DIE OF PERSECUTION SINCE YOU HAVE NO MIRACLES"
4270 GOTO 5170
4280 GOTO 4710
4290 PRINT "FOLLOWERS GETS SWAMPED CROSSING SPIRITUAL MOUNTAINS--LOSE HOLY SPIRIT AND ARMOR OF GOD"
4300 F=F-30
4310 C=C-20
4320 M=M-20-20*RND(-1)
4330 GOTO 4710
4340 PRINT "WILD PAGANS ATTACK!"
4350 GOSUB 6140
4360 IF B>39 THEN 4410
4370 PRINT "YOU WERE TOO LOW ON BIBLES--"
4380 PRINT "THE POLYTHEISTS OVERPOWERED YOU"
4390 K8=1
4400 GOTO 5120
4410 IF B1>2 THEN 4440
4420 PRINT "NICE EVANGELIZIN' PARDNER---THEY DIDN'T CONVERT MUCH"
4430 GOTO 4450
4440 PRINT "SLOW ON THE PREACHIN' ---THEY GOT AT YOUR HOLY SPIRIT AND ARMOR OF GOD"
4450 B=B-20*B1
4460 C=C-B1*4
4470 F=F-B1*8
4480 GOTO 4710
4490 PRINT "COLD SPIRITUAL WEATHER---BRRRRRRR!--YOU ";
4500 IF C>22+4*RND(-1) THEN 4530
4510 PRINT "DON'T ";
4520 C1=1
4530 PRINT "HAVE ENOUGH ARMOR OF GOD TO KEEP YOU FAITHFUL"
4540 IF C1=0 THEN 4710
4550 GOTO 6300
4560 PRINT "SPIRITUAL HAIL STORM---SUPPLIES DAMAGED"
4570 M=M-5-RND(-1)*10
4580 B=B-200
4590 M1=M1-4-RND(-1)*3
4600 GOTO 4710
4610 IF E=1 THEN 6300
4620 IF E=3 THEN 4650
4630 IF RND(-1)>.25 THEN 6300
4640 GOTO 4710
4650 IF RND(-1)<.5 THEN 6300
4660 GOTO 4710
4670 PRINT "HELPFUL GENTILES SHOW YOU WHERE TO FIND MORE HOLY SPIRIT"
4680 F=F+14
4690 GOTO 4710
4700 REM ^*MOUNTAINS^*
4710 IF M <= 950 THEN 1230
4720 IF RND(-1)*10>9-((M/100-15)^2+72)/((M/100-15)^2+12) THEN 4860
4730 PRINT "RUGGED SPIRITUAL MOUNTAINS"
4740 IF RND(-1)>.1 THEN 4780
4750 PRINT "YOU GOT SPIRITUALLY LOST---LOSE VALUABLE TIME TRYING TO GO BACK ON GOD'S PATH!"
4760 M=M-60
4770 GOTO 4860
4780 IF RND(-1)>.11 THEN 4840
4790 PRINT "SHOES DAMAGED!---LOSE TIME AND SUPPLIES"
4800 M1=M1-5
4810 B=B-200
4820 M=M-20-30*RND(-1)
4830 GOTO 4860
4840 PRINT "THE GOING GETS SLOW"
4850 M=M-45-RND(-1)/.02
4860 IF F1=1 THEN 4900
4870 F1=1
4880 IF RND(-1)<.8 THEN 4970
4890 PRINT "YOU MADE IT SAFELY THROUGH INDO-PARTHIAN KINGDOM--NO PERSECUTION"
4900 IF M<1700 THEN 4940
4910 IF F2=1 THEN 4940
4920 F2=1
4930 IF RND(-1)<.7 THEN 4970
4940 IF M>950 THEN 1230
4950 M9=1
4960 GOTO 1230
4970 PRINT "SLIGHT PERSECUTION IN THE INDO-PARTHIAN KINGDOM--TIME AND SUPPLIES LOST"
4980 L1=1
4990 F=F-25
5000 M1=M1-10
5010 B=B-300
5020 M=M-30-40*RND(-1)
5030 IF C<18+2*RND(-1) THEN 6300
5040 GOTO 4940
5050 REM ^*DYING^*
5060 PRINT "YOU RAN OUT OF THE HOLY SPIRIT AND SPIRITUALLY STARVED TO DEATH"
5070 GOTO 5170
5080 LET T=0
5090 PRINT "YOU CAN'T AFFORD JESUS"
5100 GOTO 5120
5110 PRINT "YOU RAN OUT OF MIRACLES"
5120 PRINT "YOU DIED OF ";
5130 IF K8=1 THEN 5160
5140 PRINT "DYSENTERY"
5150 GOTO 5170
5160 PRINT "SPIRITUAL INJURIES"
5170 PRINT
5180 PRINT "DUE TO YOUR UNFORTUNATE SITUATION, THERE ARE A FEW"
5190 PRINT "FORMALITIES WE MUST GO THROUGH"
5200 PRINT
5210 PRINT "WOULD YOU LIKE A MINISTER?"
5220 INPUT C$
5225 C$=UCASE$(C$)
5230 PRINT "WOULD YOU LIKE A FANCY FUNERAL?"
5240 INPUT C$
5245 C$=UCASE$(C$)
5250 PRINT "WOULD YOU LIKE US TO INFORM YOUR NEXT OF KIN?"
5260 INPUT C$
5265 C$=UCASE$(C$)
5270 IF C$="YES" THEN 5310
5280 PRINT "BUT YOUR AUNT SADIE IN JERUSALEM IS REALLY WORRIED ABOUT YOU"
5290 PRINT
5300 GOTO 5330
5310 PRINT "THAT WILL BE 5 DENARII FOR THE MESSENGER CHARGE."
5320 PRINT
5330 PRINT "WE THANK YOU FOR THIS INFORMATION AND WE ARE SORRY YOU"
5340 PRINT "DIDN'T MAKE IT TO THE GREAT EMPIRE OF INDIA"
5350 PRINT "BETTER LUCK NEXT TIME"
5360 PRINT
5370 PRINT
5380 PRINT TAB(30);"SINCERELY"
5390 PRINT
5400 PRINT TAB(17);"THE MYLAPORE CHAMBER OF COMMERCE"
5410 STOP
5420 REM ^*FINAL TURN^*
5430 F9=(3400-M2)/(M-M2)
5440 F=F+(1-F9)*(8+5*E)
5450 PRINT
5460 REM ^BELLS IN LINES 5470,5480^
5470 PRINT !"\007YOU FINALLY ARRIVED AT MYLAPORE"
5480 PRINT !"\007AFTER 3400 LONG MILES---HOORAY!!!!!"
5490 PRINT !"\007A REAL MISSIONARY!"
5491 PRINT "AFTER LEARNING ABOUT THE DIFFERENT RELIGIONS, YOU REALISE THE"
5492 PRINT "MANY SIMILARITIES BETWEEN THEM AND HOW THEY ALL CONNECT TO EACH OTHER"
5493 PRINT "YOU APPRECIATE THE PHILOSOPHICAL CONCEPTS AND OVERARCHING MORALS WITHIN THEM"
5494 PRINT "AND RECOGNISE THE BEST WAY OF EVANGELIZING THEM IS TO LISTEN TO THEM"
5495 PRINT "SO YOU CAN TALK TO THEM BASED ON THEIR BASE PRINCIPLES AND BELIEFS"
5496 PRINT "YOU BECOME A GREAT MISSIONARY IN INDIA AND ESTABLISH SEVEN CHURCHES!"
5500 PRINT
5510 F9=INT(F9*14)
5520 D3=D3*14+F9
5530 F9=F9+1
5540 IF F9<5 THEN 5560
5550 F9=F9-7
5560 ON F9 GOTO 5570,5590,5610,5630,5650,5690
5570 PRINT "MONDAY ";
5580 GOTO 5700
5590 PRINT "TUESDAY ";
5600 GOTO 5700
5610 PRINT "WEDNESDAY ";
5620 GOTO 5700
5630 PRINT "THURSDAY ";
5640 GOTO 5700
5650 PRINT "FRIDAY ";
5660 GOTO 5700
5670 PRINT "SATURDAY ";
5680 GOTO 5700
5690 PRINT "SUNDAY ";
5700 IF D3>124 THEN 5740
5710 D3=D3-93
5720 PRINT "JULY ";D3;" 72 AD"
5730 GOTO 5920
5740 IF D3>155 THEN 5780
5750 D3=D3-124
5760 PRINT "AUGUST ";D3;" 72 AD"
5770 GOTO 5920
5780 IF D3>185 THEN 5820
5790 D3=D3-155
5800 PRINT "SEPTEMBER ";D3;" 72 AD"
5810 GOTO 5920
5820 IF D3>216 THEN 5860
5830 D3=D3-185
5840 PRINT "OCTOBER ";D3;" 72 AD"
5850 GOTO 5920
5860 IF D3>246 THEN 5900
5870 D3=D3-216
5880 PRINT "NOVEMBER ";D3;" 72 AD"
5890 GOTO 5920
5900 D3=D3-246
5910 PRINT "DECEMBER ";D3/4;" 72 AD"
5920 PRINT
5930 PRINT "HOLY SPIRIT","BIBLES","ARMOR OF GOD","MISC. SUPP.","DENARII"
5940 IF B>0 THEN 5960
5950 LET B=0
5960 IF C>0 THEN 5980
5970 LET C=0
5980 IF M1>0 THEN 6000
5990 LET M1=1
6000 IF T>0 THEN 6020
6010 LET T=0
6020 IF F>0 THEN 6040
6030 LET F=0
6040 PRINT INT(F),INT(B),INT(C),INT(M1),T
6050 PRINT
6060 PRINT TAB(11); "   GOD THE FATHER SENDS YOU HIS"
6070 PRINT TAB(17); "HEARTIEST CONGRATULATIONS"
6080 PRINT
6090 PRINT TAB(11);"AND WISHES YOU A PROSPEROUS LIFE AHEAD"
6100 PRINT
6110 PRINT TAB(22);"AT YOUR NEW HOME"
6111 PRINT TAB(22);"BUT YOU GET MARTYRED ANYWAYS"
6120 STOP
6130 REM ^*SHOOTING SUB-ROUTINE^*
6131 REM THE METHOD OF TIMING THE SHOOTING (LINES 6210-6240)
6132 REM WILL VARY FROM SYSTEM TO SYSTEM.  FOR EXAMPLE, H-P
6133 REM USERS WILL PROBABLY PREFER TO USE THE 'ENTER' STATEMENT.
6134 REM IF TIMING ON THE USER'S SYSTEM IS HIGHLY SUSCEPTIBLE
6135 REM TO SYSTEM RMESPONSE TIME, THE FORMULA IN LINE 6240 CAN
6136 REM BE TAILORED TO ACCOMODATE THIS BY EITHER INCREASING
6137 REM OR DECREASING THE 'SHOOTING' TIME RECORDED BY THE SYSTEM
6140 DIM S$(5)
6150 S$(1)="CHRISTIANITY"
6160 S$(2)="HERMENEUTICS"
6170 S$(3)="RESURRECTION"
6180 S$(4)="JESUS"
6190 S6=INT(RND(-1)*4+1)
6200 PRINT "TYPE "; S$(S6)
6210 DIM B3 as DOUBLE
6215 B3 = Timer 'Seconds
6220 INPUT C$
6225 C$=UCASE$(C$)
6230 B1 = TIMER
6240 B1=((B1-B3)*1)-(D9-1)
6250 PRINT B1
6255 IF B1>0 THEN 6260
6260 IF C$=S$(S6) THEN 6280
6270 B1=9
6280 RETURN
6290 REM ^*ILLNESS SUB-ROUTINE^*
6300 IF 100*RND(-1)<10+35*(E-1) THEN 6370
6310 IF 100*RND(-1)<100-(40/4^(E-1)) THEN 6410
6320 PRINT "SERIOUS PERSECUTION---"
6330 PRINT "YOU MUST STOP FOR MIRACULAR ATTENTION"
6340 M1=M1-10
6350 S4=1
6360 GOTO 6440
6370 PRINT "MILD PERSECUTION---MIRACLE USED"
6380 M=M-5
6390 M1=M1-2
6400 GOTO 6440
6410 PRINT "BAD PERSECUTION---MIRACLE USED"
6420 M=M-5
6430 M1=M1-2
6440 IF M1<0 THEN 5110
6450 IF L1=1 THEN 4940
6460 GOTO 4710
6470 REM ^*IDENTIFICATION OF VARIABLES IN THE PROGRAM^*
6480 REM A = AMOUNT SPENT ON FOLLOWER
6490 REM B = AMOUNT SPENT ON BIBLES
6500 REM B1 = ACTUAL RESPONSE TIME FOR INPUTTING "JESUS"
6510 REM B3 = CLOCK TIME AT START OF INPUTTING "JESUS"
6520 REM C = AMOUNT SPENT ON ARMOR OF GOD
6530 REM C1 = FLAG FOR INSUFFICIENT ARMOR OF GOD IN PAGAN CLIMATE
6540 REM C$ = YES/NO RESPONSE TO QUESTIONS
6550 REM D1 = COUNTER IN GENERATING EVENTS
6560 REM D3 = TURN NUMBER FOR SETTING DATE
6570 REM D4 = CURRENT DATE
6580 REM D9 = CHOICE OF EVANGELIZING EXPERTISE LEVEL
6590 REM E = CHOICE OF HOLY SPIRITING
6600 REM F = AMOUNT SPENT ON HOLY SPIRIT
6610 REM F1 = FLAG FOR CLEARING SOUTH PASS
6620 REM F2 = FLAG FOR CLEARING BLUE MOUNTAINS
6630 REM F9 = FRACTION OF 2 WEEKS TRAVELED ON FINAL TURN
6640 REM K8 = FLAG FOR PERSECUTION
6650 REM L1 = FLAG FOR WARZONE
6660 REM M = TOTAL MILEAGE WHOLE TRIP
6670 REM M1 = AMOUNT SPENT ON MISCELLANEOUS SUPPLIES
6680 REM M2 = TOTAL MILEAGE UP THROUGH PREVIOUS TURN
6690 REM M9 = FLAG FOR CLEARING SOUTH PASS IN SETTING MILEAGE
6700 REM P = AMOUNT SPENT ON ITEMS AT CITY
6710 REM R1 = RANDOM NUMBER IN CHOOSING EVENTS
6720 REM S4 = FLAG FOR PERSECUTION
6730 REM S5 = ""HOSTILITY OF PAGANS"" FACTOR
6740 REM S6 = SHOOTING WORD SELECTOR
6750 REM S$ = VARIATIONS OF SHOOTING WORD
6760 REM T = CASH LEFT OVER AFTER INITIAL PURCHASES
6770 REM T1 = CHOICE OF TACTICES WHEN PERSECUTED
6780 REM X = CHOICE OF ACTION FOR EACH TURN
6790 REM X1 = FLAG FOR CITY OPTION
6800 END